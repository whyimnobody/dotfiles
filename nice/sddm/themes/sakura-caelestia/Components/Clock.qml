// Config created by Keyitdev https://github.com/Keyitdev/sddm-astronaut-theme
// Copyright (C) 2022-2025 Keyitdev
// Based on https://github.com/MarianArlt/sddm-sugar-dark
// Distributed under the GPLv3+ License https://www.gnu.org/licenses/gpl-3.0.html

import QtQuick 2.15
import QtQuick.Controls 2.15

Item {
    id: clock

    Column {
        id: clockContent

        anchors.fill: parent
        spacing: root.font.pointSize * 0.1

        Label {
            id: headerTextLabel

            anchors.horizontalCenter: parent.horizontalCenter

            font.pointSize: root.font.pointSize * 1.4
            color: config.HeaderTextColor
            renderType: Text.QtRendering
            text: config.HeaderText
        }

        Label {
            id: timeLabel

            anchors.horizontalCenter: parent.horizontalCenter

            font.pointSize: root.font.pointSize * 3.7
            font.bold: true
            color: config.TimeTextColor
            renderType: Text.QtRendering

            function updateTime() {
                text = new Date().toLocaleTimeString(Qt.locale(config.Locale), config.HourFormat == "long" ? Locale.LongFormat : config.HourFormat !== "" ? config.HourFormat : Locale.ShortFormat)
            }
        }

        Label {
            id: dateLabel

            anchors.horizontalCenter: parent.horizontalCenter

            color: config.DateTextColor
            font.pointSize: root.font.pointSize * 1.1
            font.bold: true
            renderType: Text.QtRendering

            function updateTime() {
                text = new Date().toLocaleDateString(Qt.locale(config.Locale), config.DateFormat == "short" ? Locale.ShortFormat : config.DateFormat !== "" ? config.DateFormat : Locale.LongFormat)
            }
        }

        Timer {
            interval: 1000
            repeat: true
            running: true
            onTriggered: {
                dateLabel.updateTime()
                timeLabel.updateTime()
            }
        }

        Component.onCompleted: {
            dateLabel.updateTime()
            timeLabel.updateTime()
        }
    }

    Rectangle {
        anchors.fill: clockContent
        anchors.margins: -8
        color: config.ClockBackgroundColor
        opacity: config.ClockBackgroundOpacity
        border.color: config.ClockBorderColor
        border.width: config.ClockBorderWidth
        radius: config.ClockBorderRadius
        z: -1
    }

}
